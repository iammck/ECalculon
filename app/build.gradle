apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.mck.ecalculon"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 10
        versionName "basic"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        travis {
            // if building with the ci service, this env var exists.
            if (System.getenv("KEY_ALIAS") != null){
                // specific to the ci build
                storeFile file("keystore/ecalculon.keystore")
                storePassword System.getenv("KSPWD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PWD")
            }
        }

        console {
            // we can find a console, is it also not the travis console?
            if (System.console() != null && System.getenv("KEY_ALIAS") == null){
                storeFile file("keystore/ecalculon.keystore")
                storePassword new String(System.console()
                        .readPassword("\n\$ Enter keystore password: "))
                keyAlias new String(System.console()
                        .readLine("\n\$ Enter key alias: "))
                keyPassword new String(System.console()
                        .readPassword("\n\$ Enter key password: "))
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // if building with the ci service
            if (System.getenv("KEY_ALIAS") != null){
                signingConfig signingConfigs.travis
            } else if ( System.console() != null){
                signingConfig signingConfigs.console
            }
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
}

play {
    track = 'alpha' // production, beta
    serviceAccountEmail = System.getenv("SAEMAIL")
    pk12File = file("keystore/ecalculonp12.p12")
}

task publish(type: com.mck.ecalculon.PublishAlphaTask){
    email play.serviceAccountEmail
    pk12 play.pk12File
}

publish.dependsOn assembleRelease